print =: [: stdout ,&LF

NB. Энтропия совокупности / Количество информации для всей совокупности
I =: [: -@(+/) (*2&^.)"0

NB. Создаёт массив случайных вероятностей сообщений, сумма которых всегда равна 1.0
Px =: [: (%+/)@:? $&0

NB. Матрица переходов, где вероятность правильной передачи информации более
NB. 70%. Принимает количество сообщений
correct_transitions =: [: (*0.7+0.3*?@:-.)@:= i.

PX_Y =: correct_transitions ([+] * [:-. +/@[) [: (%+/)"1 ?@=@i.

NB. Проверка что сумма вероятностей = 1
verb : 0 ''
    assert. +/"1 PX_Y amount
)

NB. Вычисляет массив выходных сообщений, принимает массив входных сообщений и
NB. матрицу переходов (т.е. Px u PX_Y)
Py=: +/ .*

NB. Вычисляет матрицу совместных вероятностей. Принимает массив выходных
NB. сообщений и матрицу переходов (т.е. Py u PX_Y)
PXY=: *"1

NB. Вычисляет условную энтропию. Принимает матрицу совместных вероятностей и
NB. матрицу переходов (т.е. PXY u PX_Y)
HX_Y=: [: -@(+/)@(+/) [ * 2 ^. ]

NB. Вычисляет среднее количество информации, получаемое при неполной
NB. достоверности сообщений. Принимает условную энтропия и массив входных
NB. сообщений (т.е. HX_Y u Px)
IXY=: -~I

NB. --------------

NB. Одно испытание
trial =: monad : 0
    print ''
    print 'Испытание №', ": y
    print ' Вероятности каждых из возможных сообщений на входе, P(X):'
    print '  ', 0j5": inputs =. Px amount
    print ' Матрица переходов, P(X/Y):'
    stdout ,LF,.~'  ',"(1) 0j5": transitions =. PX_Y amount
    print ' Вероятности каждого из возможных сообщений на выходе, P(Y):'
    print '  ', 0j5": outputs =. inputs Py transitions
    print ' Матрица совместных вероятностей, P(X,Y):'
    stdout ,LF,.~'  ',"(1) 0j5": joint_provs =. outputs PXY transitions
    print ' Энтропия на входе, H(X): ', 0j5": I inputs
    print ' Условная энтропия, H(X/Y): ', 0j5": conditional =. joint_provs HX_Y transitions
    print ' Среднее количество информации при неполной достоверности сообщения, I(X,Y): ', 0j5": val =. conditional IXY inputs
    val
)

NB. Запускает испытание несколько раз, вычисляет минимум и среднее
'minimum maximum average'=: (<./ ; >./ ; trials %~ +/) trial"0] 1 + i. trials

print '───────────────────────────────────────────────────────────'
print 'Среднее количество информации, I(X,Y), в совокупности всех экспериментов: ', 0j5": average
print 'Максимальное количество информации: ', 0j5": maximum
print 'Минимальное количество информации: ', 0j5": minimum
