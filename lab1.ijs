print =: [: stdout ,&LF

NB. Энтропия совокупности / Количество информации для всей совокупности
I =: [: -@(+/) (*2&^.)"0

NB. Создаёт массив случайных вероятностей сообщений, сумма которых всегда равна 1.0
message_distribution =: [: (%+/) 1 (+?) $&100

NB. Одно испытание
trial =: monad : 0
    print ''
    print 'Испытание №', ": y
    print '  Количество возможных сообщений: ', ": amount
    print '  Вероятности каждых из возможных сообщений:'
    print '  {','}',~([,', ',])&:>/ 0j5&":&.> ;/ msgs =. message_distribution amount
    print '  Количество информации совокупности сообщений: ', 0j5": val=: I msgs
    val
)

NB. Запускает испытание несколько раз, вычисляет минимум и среднее
'minimum average'=: (<./ ; trials %~ +/) trial"0] 1 + i. trials

print '───────────────────────────────────────────────────────────'
print 'Минимальное количество информации в совокупности сообщений всех экспериментов: ', 0j5": minimum
print 'Среднее количество информации в совокупности сообщений всех экспериментов: ', 0j5": average
print 'Максимальная возможная энтропия совокупности сообщений: ', 0j5": I@:($%) amount
