print =: [: stdout ,&LF

d_min=: 3

NB. Соотношение: 2^k <= 2^n/(1+n)
code_length=: -~ (- 2&^.@>:)
NB. Вычисляет минимальное количество проверочных разрядов. Принимает на вход k
NB. (количество информационных разрядов)
p=: -~ {{ >. (- y&code_length)^:_] 1 }}

NB. Вычисляет проверочную подматрицу для определённого (d_min-1) u p
H_p=: (] #~ [ <: +/"1@]) i.@>:&.:#.@:$&1

NB. Вычисляет проверочные символы для сообщения и транспонированной проверочной
NB. подматрицы ((|:H_p) u msg)
code=: 2 | +/ .*"1

NB. Из информационных битов вычисляет полное сообщение, включающее проверочные
NB. биты. Принимает проверочную подматрицу и сообщение (H_p u p)
encode=: ] , |:@[code]

NB. Вычисляет полную проверочную матрицу из проверочной подматрицы (u H_p)
correct=: , =@i.@{:@$

NB. Берёт проверочную подматрицу и закодированное сообщение и вычисляет
NB. синдром ошибки (H_p u msg)
syndrome=: ({.~ data_length - #)@] ~: (|:@[ code data_length&{.@])

NB. Берёт проверочную подматрицу и закодированное сообщение и исправляет
NB. ошибку в сообщении (H u msg)
fix=: {{ -.&.(idx&{)^:((#y) > idx=. (correct x) i. x syndrome y) y }}

NB. --------------

print 'Количество информационных разрядов: ', ": data_length
print 'Количество проверочных разрядов: ', ": p data_length
print 'Проверочная матрица (отправитель и получатель её вычисляют самостоятельно):'
stdout ,LF,.~' ',"(1) ": correct H=: data_length {. (d_min-1) H_p p data_length

NB. Одно испытание
trial =: monad : 0
    print ''
    print 'Испытание №', ": y
    print ' Сообщение:'
    print '  ', ": msg=. ?data_length$2
    print ' Кодированое сообщение:'
    print '  ', ": encoded=. H encode msg
    print ' ...Отправляем сообщение...'
    faulty=. encoded
    if. ?1+#encoded do.
        faulty=. -.&.((?#encoded)&{) faulty
        print '  Ошибка: бит ',' поменял значение',~ ": 1+I. faulty~:encoded
    else.
        print '  Ошибки не произошло'
    end.
    print ' ...Получаем сообщение...'
    print ' Полученное сообщение:'
    print '  ', ": faulty
    print ' Синдром ошибки:'
    print '  ', ": synd=. H syndrome faulty
    if. (#faulty) > idx=. (correct H) i. synd do.
        print ' Порядковый номер неправильного бита: ', ": 1+idx
    end.
    print ' Исходное сообщение:'
    print '  ', ": fixed=. H fix faulty
    NB. Подтверждаем что сообщения совпадают
    assert. encoded -: fixed
    1
)

NB. Все испытания
trial"0 >:i.trials
